
#Region ServiceAPI

Функция ФизическоеЛицоИзПодписи(Подпись) Экспорт
	
	Если НЕ ЗначениеЗаполнено(Подпись) Тогда
		
		Возврат Неопределено;
		
	КонецЕсли;
	
	Возврат Подпись.ФизическоеЛицо;
	
КонецФункции

Функция ШаблонТаблицыФаксимильныхПодписей() ЭКспорт
	
	ОписаниеФорм = Новый ТаблицаЗначений;
	ОписаниеФорм.Колонки.Добавить("УдалитьЗначениеПоУмолчанию", Новый ОписаниеТипов("Булево"),					НСтр("ru ='Подписывать по умолчанию факсимильной подписью (Булево)'"));
	ОписаниеФорм.Колонки.Добавить("ЗначениеПоУмолчанию",	Новый ОписаниеТипов("ПеречислениеСсылка.ДаНет"),	НСтр("ru ='Подписывать по умолчанию факсимильной подписью (Перечисление)'"));
	ОписаниеФорм.Колонки.Добавить("Организация",			Новый ОписаниеТипов("СправочникСсылка.Организации"),НСтр("ru ='Организация'"));
	ОписаниеФорм.Колонки.Добавить("Документ",				Новый ОписаниеТипов("Строка"),						НСтр("ru ='Документ (имя метаданных)'"));
	ОписаниеФорм.Колонки.Добавить("ПредставлениеДокумента",	Новый ОписаниеТипов("Строка"),						НСтр("ru ='Представление документа'"));
	ОписаниеФорм.Колонки.Добавить("ИмяМакетаПФ",			Новый ОписаниеТипов("Строка"),						НСтр("ru ='Имя макета печатной формы'"));
	ОписаниеФорм.Колонки.Добавить("ПредставлениеПФ",		Новый ОписаниеТипов("Строка"),						НСтр("ru ='Представление печатной формы'"));
	
	Возврат ОписаниеФорм;
	
КонецФункции

Процедура ДобавитьОписаниеПечатнойФормы(ОписаниеФорм, ПодписыватьПоУмолчанию, Организация, Документ, ПечатнаяФорма)
	
	СтрокаОписания							= ОписаниеФорм.Добавить();
	СтрокаОписания.УдалитьЗначениеПоУмолчанию= ПодписыватьПоУмолчанию;
	СтрокаОписания.ЗначениеПоУмолчанию		= ?(ПодписыватьПоУмолчанию = Истина, Перечисления.ДаНет.Да, Перечисления.ДаНет.Нет);
	СтрокаОписания.Организация				= Организация;
	СтрокаОписания.Документ					= Документ.Имя;
	СтрокаОписания.ПредставлениеДокумента	= ?(Документ.Имя = "ЗаказПокупателя", НСтр("ru ='Заказ, Заказ-наряд'"), Документ.Синоним);
	СтрокаОписания.ИмяМакетаПФ				= ПечатнаяФорма.Имя;
	СтрокаОписания.ПредставлениеПФ			= ПечатнаяФорма.Синоним;
	
КонецПроцедуры

Процедура ПечатныеФормыПоддерживающиеФаксимильнуюПодпись(ОписаниеФорм, Организация) Экспорт
	
	МетаданныеДокумента = Метаданные.Документы.СчетНаОплату;
	ДобавитьОписаниеПечатнойФормы(ОписаниеФорм, Истина, Организация, МетаданныеДокумента, Метаданные.Обработки.ПечатьСчетНаОплату.Макеты.ПФ_MXL_СчетНаОплату);
	
	МетаданныеДокумента = Метаданные.Документы.ЗаказПокупателя;
	ДобавитьОписаниеПечатнойФормы(ОписаниеФорм, Истина, Организация, МетаданныеДокумента, Метаданные.Обработки.ПечатьСчетНаОплату.Макеты.ПФ_MXL_СчетНаОплату);
	
	МетаданныеДокумента = Метаданные.Документы.РасходнаяНакладная;
	ДобавитьОписаниеПечатнойФормы(ОписаниеФорм, Истина, Организация, МетаданныеДокумента, Метаданные.Обработки.ПечатьСчетНаОплату.Макеты.ПФ_MXL_СчетНаОплату);
	
КонецПроцедуры

Процедура ДобавитьУсловноеОформлениеСписка(ЭтаФорма, СтруктураПараметров) Экспорт
	
	ИмяПоляПроверки = СтруктураПараметров.ИмяПоляПроверки;
	ИмяПоляОформления = СтруктураПараметров.ИмяПоляОформления;
	
	//(1)
	ЭлементУсловногоОформления = ЭтаФорма.УсловноеОформление.Элементы.Добавить();
	ЭлементУсловногоОформления.Оформление.УстановитьЗначениеПараметра("ТолькоПросмотр", Ложь);
	
	ГруппаОтбора = ЭлементУсловногоОформления.Отбор.Элементы.Добавить(Тип("ГруппаЭлементовОтбораКомпоновкиДанных"));
	ГруппаОтбора.ТипГруппы = ТипГруппыЭлементовОтбораКомпоновкиДанных.ГруппаИ;
	
	//ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ГруппаОтбора, ИмяПоляПроверки, ВидСравненияКомпоновкиДанных.Заполнено);
	//ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ГруппаОтбора, ИмяПоляПроверки, ВидСравненияКомпоновкиДанных.НеРавно, Справочники.СтраныМира.Россия);
	
	//Если СтруктураПараметров.Свойство("ДополнительноеПолеПроверки") Тогда
	//	
	//	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ГруппаОтбора, СтруктураПараметров.ДополнительноеПолеПроверки, ВидСравненияКомпоновкиДанных.Равно, Ложь);
	//	
	//КонецЕсли;
	
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУсловногоОформления.Поля, ИмяПоляОформления);
	
КонецПроцедуры

#Область ПодписантыИРасшифровки

Процедура ЗаполнитьФаксимилеВОбластиМакета(ОбластьМакета, ДанныеОбъекта, ПодписиИФаксимиле, Ошибки) Экспорт
	
	Для каждого ЭлементСоответствия Из ПодписиИФаксимиле Цикл
		
		ПлашкаПодписи = ПечатьДокументовУНФ.ПолучитьПлашкуПодписиБезопастно(ОбластьМакета, ЭлементСоответствия.Ключ, , Ошибки);
		Если ПлашкаПодписи = Неопределено Тогда
			
			Продолжить;
			
		КонецЕсли;
		
		Если ЗначениеЗаполнено(ДанныеОбъекта[ЭлементСоответствия.Значение]) Тогда
			
			ДвоичныеДанные = ПрисоединенныеФайлы.ПолучитьДвоичныеДанныеФайла(ДанныеОбъекта[ЭлементСоответствия.Значение]);
			Если ЗначениеЗаполнено(ДвоичныеДанные) Тогда
				
				ПлашкаПодписи.Картинка = Новый Картинка(ДвоичныеДанные);
				
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#Область ЗаполнениеНовых

Функция ДокументыСПравиламиЗаполнения() Экспорт
	
	//Пример:
	// Метаданные.Документы.ИнвентаризацияЗапасов.Имя
	//		или
	// "ИнвентаризацияЗапасов"
	
	ДокументыСПравилами = Новый Массив;
	ДокументыСПравилами.Добавить("ИнвентаризацияЗапасов");
	ДокументыСПравилами.Добавить("ПриходныйОрдер");
	ДокументыСПравилами.Добавить("СверкаВзаиморасчетов");
	ДокументыСПравилами.Добавить("ЧекККМ");
	
	Возврат ДокументыСПравилами;
	
КонецФункции

Функция НадоВключитьФаксимиле(Организация, МетаданныеОбъекта) Экспорт
	
	ИмяВидаДокумента = "";
	Если ТипЗнч(МетаданныеОбъекта) = Тип("Строка") Тогда
		
		ИмяВидаДокумента = МетаданныеОбъекта;
		
	Иначе
		
		ИмяВидаДокумента = МетаданныеОбъекта.Имя;
		
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Организация", Организация);
	Запрос.УстановитьПараметр("ИмяВидаДокумента", ИмяВидаДокумента);
	
	Запрос.Текст = 
	"Выбрать Первые 1 ФаксимилеНастройки.ЗначениеПоУмолчанию
	|Из РегистрСведений.ФаксимилеНастройки КАК ФаксимилеНастройки
	|Где ФаксимилеНастройки.Организация = &Организация И ФаксимилеНастройки.Документ = &ИмяВидаДокумента";
	
	Выборка = Запрос.Выполнить().Выбрать();
	Возврат ?(Выборка.Следующий(), Выборка.ЗначениеПоУмолчанию, Истина);
	
КонецФункции

Процедура ОбработкаЗаполненияПодписей(ОбъектЗаполнения, Организация = Неопределено, СтруктурнаяЕдиница = Неопределено, Касса = Неопределено) Экспорт
	
	МетаданныеОбъекта = ОбъектЗаполнения.Метаданные();
	
	СоответствиеИменРеквизитовПодписей = Справочники.Подписи.ПереченьРеквизитовПодписейВОбъекте(МетаданныеОбъекта.Имя);
	Если СоответствиеИменРеквизитовПодписей = Неопределено Тогда
		
		ЗаполнитьПодписиПоСтандартномуАлгоритму(ОбъектЗаполнения, МетаданныеОбъекта, Организация, СтруктурнаяЕдиница, Касса);
		
	Иначе
		
		ЗаполнитьПодписиПоИндивидуальнымПравилам(СоответствиеИменРеквизитовПодписей, ОбъектЗаполнения, МетаданныеОбъекта);
		
	КонецЕсли;
	
	Если ОбщегоНазначения.ЕстьРеквизитОбъекта("ИспользоватьФаксимиле", МетаданныеОбъекта) Тогда
		
		ОбъектЗаполнения.ИспользоватьФаксимиле = НадоВключитьФаксимиле(?(Организация = Неопределено, ОбъектЗаполнения.Организация, Организация), МетаданныеОбъекта);
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ЗаполнитьПодписиПоСтандартномуАлгоритму(ОбъектЗаполнения, МетаданныеОбъекта, Организация, СтруктурнаяЕдиница, Касса)
	
	ЗначениеРеквизитов = Новый Структура("ПодписьРуководителя, ПодписьГлавногоБухгалтера, ПодписьКладовщика, ПодписьМОЛ, ПодписьКассира");
	
	Если ЗначениеЗаполнено(Организация) Тогда
		
		ЗначениеРеквизитов.ПодписьРуководителя = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Организация, "ПодписьРуководителя");
		ЗначениеРеквизитов.ПодписьГлавногоБухгалтера = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Организация, "ПодписьГлавногоБухгалтера");
		
	ИначеЕсли ОбщегоНазначения.ЕстьРеквизитОбъекта("Организация", МетаданныеОбъекта) Тогда
		
		ЗначениеРеквизитов.ПодписьРуководителя = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ОбъектЗаполнения.Организация, "ПодписьРуководителя");
		ЗначениеРеквизитов.ПодписьГлавногоБухгалтера = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ОбъектЗаполнения.Организация, "ПодписьГлавногоБухгалтера");
		
	КонецЕсли;
	
	Если ЗначениеЗаполнено(СтруктурнаяЕдиница) Тогда
		
		ЗначениеРеквизитов.ПодписьКладовщика = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(СтруктурнаяЕдиница, "ПодписьМОЛ");
		ЗначениеРеквизитов.ПодписьМОЛ = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(СтруктурнаяЕдиница, "ПодписьМОЛ");
		
	ИначеЕсли ОбщегоНазначения.ЕстьРеквизитОбъекта("СтруктурнаяЕдиница", МетаданныеОбъекта) Тогда
		
		ЗначениеРеквизитов.ПодписьКладовщика = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ОбъектЗаполнения.СтруктурнаяЕдиница, "ПодписьМОЛ");
		ЗначениеРеквизитов.ПодписьМОЛ = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ОбъектЗаполнения.СтруктурнаяЕдиница, "ПодписьМОЛ");
		
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Касса) Тогда
		
		ЗначениеРеквизитов.ПодписьКассира = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Касса, "ПодписьКассира");
		
	ИначеЕсли ОбщегоНазначения.ЕстьРеквизитОбъекта("Касса", МетаданныеОбъекта) Тогда
		
		ЗначениеРеквизитов.ПодписьКассира = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ОбъектЗаполнения.Касса, "ПодписьКассира");
		
	КонецЕсли;
	
	ЗаполнитьЗначенияСвойств(ОбъектЗаполнения, ЗначениеРеквизитов);
	
КонецПроцедуры

Процедура ЗаполнитьПодписиПоИндивидуальнымПравилам(СоответствиеИменРеквизитовПодписей, ОбъектЗаполнения, МетаданныеОбъекта)
	
	ЗначениеРеквизитов = Новый Структура;
	
	Для каждого ЭлементСоответствия Из СоответствиеИменРеквизитовПодписей Цикл
		
		ЗначениеРеквизитов.Вставить(ЭлементСоответствия.Ключ, Неопределено);
		
		Если ЭлементСоответствия.Ключ = "Комиссия" Тогда
			
			ЗаполнитьКомиссию(ЗначениеРеквизитов[ЭлементСоответствия.Ключ], МетаданныеОбъекта.Имя, ОбъектЗаполнения.Организация);
			Продолжить;
			
		КонецЕсли;
		
		ЗначениеРеквизитов[ЭлементСоответствия.Ключ] = Вычислить("ОбъектЗаполнения." + ЭлементСоответствия.Значение);
		
	КонецЦикла;
	
	ЗаполнитьЗначенияСвойств(ОбъектЗаполнения, ЗначениеРеквизитов);
	
КонецПроцедуры

Процедура ЗаполнитьКомиссию(Комиссия, ИмяОбъектаЗаполнения, Организация)
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Организация", Организация);
	Запрос.УстановитьПараметр("ОтраслевойДокумент", ИмяОбъектаЗаполнения);
	
	Запрос.Текст = 
	"Выбрать Справочник.Комиссии.Ссылка КАК Комиссия
	|Где НЕ КомиссияРасформирована И Организация = &Организация И ОтраслевойДокумент = &ОтраслевойДокумент
	|Упорядочить по ДатаПодписаниеПриказа Убыв";
	
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		
		Комиссия = Выборка.Комиссия;
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#EndRegion